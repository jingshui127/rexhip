FUNCTION_BLOCK "ensy_LMC311"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      mb_addr : USInt;
   END_VAR

   VAR_IN_OUT 
      mb_query : "mb_query";
   END_VAR

   VAR 
      sb : "mb_station_block_udt";
      read1 : Struct
         air_filter_alarm : Int;
         door_open : Int;
         smoke_alarm : Int;
         motor_thermo_fuse : Int;
         heating_surface_frost_overheat : Int;
         airflow_monitor : Int;
      END_STRUCT;
      read2 : Struct
         Controller_board_temperature : Int;
         Fresh_air_intake : Int;
         Inlet_temperature_Before_heater : Int;
         Room_exhaust_temperature : Int;
         Outlet_temperature : Int;
         Condenser_temperature : Int;
         Evaporator_temperature : Int;
         Inlet_temperature_after_heater : Int;
         Outdoor_temperature : Int;
         Heating_surface_temperature : Int;
         External_room_temperature : Int;
      END_STRUCT;
      read3 : Struct
         Supply_fan_speed : Int;
         Exhaust_fan_speed : Int;
      END_STRUCT;
      write1 : Struct
         Rotor_exchanger_on : Int := 1;
         Exhaust_fan : Int := 1;
         Supply_fan : Int := 1;
      END_STRUCT;
      write2 : Struct
         on : Int := 1;
         mode : Int := 3;   // 0 : Off, 1 : Heat, 2 : Cool, 3 : Auto
         ventilation_step : Int := 2;   // 0 : Off,  1..3 : Step number
         temperature_setpoint : Int := 2100;
      END_STRUCT;
   END_VAR


BEGIN
	// Ensy - LMC311
	// Air Handling Controller - Ventilation unit
	// 
	// https://www.ventistal.no/images/INOVENT_Modbus_Manual.pdf
	// 
	// Default address: 30, (May be changed, holding register 0)
	// Buad 19200, parity even
	
	"mb_station_block_header"(sb := #sb, mb_query := #mb_query);
	#mb_query.mb_addr := #mb_addr;
	
	#mb_query.mode := #mb_query.c.read.input_reg;
	#mb_query(data_addr := 101, data_ptr := #read1);
	#mb_query(data_addr := 200, data_ptr := #read2);
	#mb_query(data_addr := 230, data_ptr := #read3);
	
	#mb_query.mode := #mb_query.c.read.holding_reg;
	#mb_query(data_addr := 130, data_ptr := #write1);
	#mb_query(data_addr := 1001, data_ptr := #write2);
	
	"mb_station_block_footer"(sb := #sb, mb_query := #mb_query);
	
END_FUNCTION_BLOCK

